plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.lopes.githubsearch"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "com.lopes.githubsearch.HiltTestRunner"
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}
dependencies {
    def paging_version = "3.0.0"
    def retrofitVersion = "2.9.0"
    def loggingInterceptor = "4.9.0"
    def roomVersion = '2.3.0'
    def coroutines = '1.5.2'
    def glide = '4.13.0'
    def cardView = '1.0.0'
    def navigation = '2.3.5'
    def appCompat = '1.4.1'
    def coreKtx = '1.7.0'
    def constraintLayout = '2.1.2'
    def materialDesign = '1.4.0'
    def jUnit = '4.+'
    def timber = '5.0.1'
    def swipeRefresh = '1.1.0'
    def mockk = '1.11.0'
    def coroutinesTest = "1.4.2"
    def archTest = "2.1.0"
    def roboletric = "4.5.1"
    def mockWebServer = "4.7.2"
    def coreKtxTest = "1.4.0"
    def hilt_version = "2.40"

    // dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // swipe refresh
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipeRefresh"

    // timber(logging)
    implementation "com.jakewharton.timber:timber:$timber"

    // room
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"

    // glide
    implementation "com.github.bumptech.glide:glide:$glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide"

    // cardView
    implementation "androidx.cardview:cardview:$cardView"

    // paging 3
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-moshi:${retrofitVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${loggingInterceptor}"

    // core
    implementation "androidx.core:core-ktx:$coreKtx"
    implementation "androidx.appcompat:appcompat:$appCompat"
    implementation "com.google.android.material:material:$materialDesign"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    // tests
    testImplementation  "junit:junit:$jUnit"
    testImplementation "io.mockk:mockk:$mockk"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesTest"
    testImplementation "androidx.arch.core:core-testing:$archTest"
    testImplementation "org.robolectric:robolectric:$roboletric"
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServer"
    testImplementation "androidx.test:core-ktx:$coreKtxTest"

    def nav_version = "2.3.0"
    def androidx_test_espresso = "3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_test_espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$androidx_test_espresso"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    debugImplementation "androidx.fragment:fragment-testing:1.5.0-alpha02"
    androidTestImplementation "io.mockk:mockk-android:1.12.2"

    def androidx_test = "1.4.0"
    androidTestImplementation "androidx.test:runner:$androidx_test"
    androidTestImplementation "androidx.test:core:$androidx_test"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"
}



